PREFIX dc: <http://purl.org/dc/terms/> 
PREFIX foaf: <http://xmlns.com/foaf/0.1/> 
PREFIX qb: <http://purl.org/linked-data/cube#> 
PREFIX skos: <http://www.w3.org/2004/02/skos/core#> 
PREFIX dad-prop: <http://semantic.digital-agenda-data.eu/def/property/>  
SELECT DISTINCT ?uri ?notation ?definition ?label ?short_label ?note ?source_label ?source_definition ?source_notes ?source_url ?groupName ?parentOrder ?innerOrder WHERE {
    { ?uri skos:notation ?notation }
    OPTIONAL { ?uri skos:prefLabel ?label }
    OPTIONAL { ?uri skos:altLabel ?short_label }
    OPTIONAL { ?uri skos:note ?note }
    OPTIONAL { ?uri skos:definition ?definition }
    ### REFACTOR HERE - START
    OPTIONAL { ?uri dc:source [
        skos:prefLabel ?source_label ;
        skos:definition ?source_definition ;
        foaf:page ?source_url ] 
    }
    OPTIONAL {?uri dc:source [skos:note ?source_notes] }
    OPTIONAL {
        ?uri dad-prop:membership ?parentNode.
        OPTIONAL {?parentNode dad-prop:order ?innerOrder} .
        ?parentNode dad-prop:member-of ?parentUri.
        ?parentUri skos:prefLabel ?groupName.
        OPTIONAL {?parentUri dad-prop:order ?parentOrder}.
    }  
    ### REFACTOR HERE - END
FILTER (
  ?uri = <http://semantic.digital-agenda-data.eu/codelist/indicator/e_igov> ||
  ?uri = <http://semantic.digital-agenda-data.eu/codelist/indicator/mbb_ltecov> ||
  ?uri = <http://semantic.digital-agenda-data.eu/codelist/indicator/gdp> ||
  ?uri = <http://semantic.digital-agenda-data.eu/codelist/indicator/foa_cit> ||
  ?uri = <http://semantic.digital-agenda-data.eu/codelist/indicator/bb_ne> ||
  ?uri = <http://semantic.digital-agenda-data.eu/codelist/indicator/mbb_penet> ||
  ?uri = <http://semantic.digital-agenda-data.eu/codelist/indicator/e_itsp2>
  )
}

LIMIT 1000
